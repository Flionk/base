bind ESCAPE [if (cleargui 1) [] [showgui main]]    // it all starts here

showmainplayerprev = 1
setpersist showmainplayerprev 1
setcomplete showmainplayerprev 1

newgui main [
    guitwo [
        if $showmainplayerprev [ guicenter [
            guicenter [ guiplayerpreview (getplayermodel) (getplayercolour -1) (getplayerteam 1) (weapselect) (getplayervanity) [showgui profilediff; showgui profile] 7.5 1 1]
            guicenter [ guitext (getplayername) ]
        ] ]
    ] [
        guilist [ guistrut 30 ]
        guicenter [ guilist [ guifont "emphasis" [
            guicenter [ guibutton "Profile" "showgui profilediff; showgui profile" ]
            guicenter [ guibutton "Benutzer Acount" "showgui useraccount" ]
            guistrut 0.5
            if (&& (isconnected) (> (getvote) 0)) [
                guicenter [ guibutton "Map Abstimmung" "showgui maps 2" ]
                guistrut 0.5
            ]
            guicenter [ guibutton (? (isonline) "Finde Server" "Online Spiel") "showservers" ]
            guicenter [ guibutton (? (isconnected) "Abstimmung von Map/Mode" "Offline Übung") "showgui maps 1" ]
            guistrut 0.5
            if (&& (isconnected) (= (gamemode) $modeidxediting)) [
                guicenter [ guibutton "editing" "showgui edit" ]
                guistrut 0.5
            ]
            guicenter [ guibutton "Options" "showgui options" ]
            guicenter [ guibutton "Chat Lobby" "showgui lobby" ]
            guicenter [ guibutton "Variablen" "showgui vars" ]
            guicenter [ guibutton "Hilfe" "showgui help" ]
            guistrut 0.5
            if (isconnected) [
                guicenter [ guibutton "Server Verlassen" "savewarnchk disconnect" ]
                guistrut 0.5
            ]
            guicenter [ guibutton "Beenden" "savewarnchk quit" ]
        ] ] ]
    ]
    cases $guirolloveraction "showservers" [
        guitooltip "Anzeige für online Server Suche"
    ] "showgui profilediff; showgui profile" [
        guitooltip "Bearbeite dein Namen, Farbe, loadout und Model Type"
    ] "showgui edit" [
        guitooltip "Zugriffsbefehle die beim Editieren einer Map verwendet werden sollen"
    ] "showgui lobby" [
        guitooltip "Schließen sie sich unsere Irc Kanal an oder siehe offene irc Tabs"
    ]  "savewarnchk disconnect" [
        guitooltip "Trennenung der aktuellen Verbindung"
    ] "showgui maps 2" [
        guitooltip "Anzeige der aktuellen Abstimmung"
    ] "showgui maps 1" [
        guitooltip (format "Wähle eine Karte/Mode Kombination zu %1" (? (isonline) "vote for" "practice on"))
    ] "showgui options" [
        guitooltip "Bearbeite deine Konfigurationsoptionen"
    ] "showgui vars" [
        guitooltip "Nach Variablen, Befehlen oder Aliasnamen suchen und eine Vielzahl von Spiel und Client Einstellungen bearbeiten."
    ] "showgui help" [
        guitooltip "Erfahre mehr über viele Aspekte des Spiels"
    ] "savewarnchk quit" [
        guitooltip "Beendet Red Eclipse"
    ]
    guitip
]

guimodes = [
    guicenter [ guitext (modedesc @arg1 @arg2 5) ]
    if (> $arg2 0) [
        loop i $mutsidxnum [
            mut = (<< 1 $i)
            if (&& [& $arg2 $mut] [! (& (mutsimplied $arg1) $mut)]) [
                muttxt = (mutsdesc $arg1 $mut 4)
                if (stringlen $muttxt) [ guicenter [ guitext (concatword "^fa" $muttxt) ] ]
            ]
        ]
    ]
]

newgui loading [
    guistayopen [ guinohitfx [
        guione [
            guifont "huge" [ guicenter [ guitext (? (&& (isloadingmap) (stringlen $maptitle)) $maptitle "Lädt") ] ]
            guifont "default" [ guicenter [ guitext (? (&& (isloadingmap) (stringlen $maptitle) (stringlen $mapauthor)) (concat "by" $mapauthor) "bitte Warten...") ] ]
            if (&& (isloadingmap) (isconnected)) [
                if (= (isloadingmap) 2) [
                    guicenter [ guitext "^fs^fylightmapping^fS" ]
                ] [
                    gname = (gamename (gamemode) (mutators) 0 32)
                    guicenter [ guitext (format "^fs^fy%1^fS" $gname) ]
                ]
            ]
            case (isloadingmap) 2 [
                guistrut 0.5
                guicenter [ guiimage "" [] 7 0 ]
            ] 1 [
                if (isconnected) [
                    guistrut 0.5
                    guifont "little" [
                        guicenter [
                            modetexs = (modetexlist (gamemode) (mutators))
                            looplist modetex $modetexs [ guiimage $modetex [] 1 0 "textures/blank" ]
                        ]
                        guimodes (gamemode) (mutators)
                    ]
                    guistrut 0.5
                    if (stringlen $connectname) [
                        guicenter [ guitext (format "^faon: ^fw%1:[%2]" $connectname $connectport) ]
                        if (&& (isconnected) (stringlen $serverdesc)) [ guifont "little" [ guicenter [ guitext (format "^"^fs%1^fS^"" $serverdesc) ] ] ]
                    ]
                ]
            ]
        ]
        guitip
    ] ]
]
